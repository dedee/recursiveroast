plugins {
    id 'java'
    id 'application'
}

group 'org.dedee'

// Get version from Git tag, fallback to 'dev' if no tag exists
version = System.getenv('GITHUB_REF_NAME') ?: getVersionFromGit()

def getVersionFromGit() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--exact-match'
            standardOutput = stdout
            ignoreExitValue = true
        }
        def tag = stdout.toString().trim()
        return tag.isEmpty() ? 'dev-SNAPSHOT' : tag.replaceFirst('^v', '')
    } catch (Exception e) {
        return 'dev-SNAPSHOT'
    }
}

repositories {
    mavenCentral()
}

application {
    mainClass = 'org.dedee.recursiveroast.ui.TurtleGraphics'
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.11'
    implementation 'org.slf4j:slf4j-simple:2.0.11'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'org.dedee.recursiveroast.ui.TurtleGraphics'
    }
    archiveBaseName = 'recursiveroast'
    archiveVersion = version
}
